plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.0'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'api'
version = '0.0.1-SNAPSHOT'

ext {
	set('springCloudVersion', "2025.0.0")
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven { url 'https://jitpack.io' }
}

dependencies {
	implementation project(':buyhood-global-core:buyhood-global-config')
	implementation project(':buyhood-category:buyhood-product-category')
	implementation project(':buyhood-category:buyhood-store-category')
	implementation project(':buyhood-cart')
	
	// spring validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	
	// spring web
	implementation 'org.springframework.boot:spring-boot-starter-web'
	
	// lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	
	// h2 database
	runtimeOnly 'com.h2database:h2'
	
	// mysql driver
	runtimeOnly 'com.mysql:mysql-connector-j'
	
	// etc
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	
	// portone
	implementation 'com.github.iamport:iamport-rest-client-java:0.2.23'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	
	// QR Code - zxing
	implementation group: 'com.google.zxing', name: 'javase', version: '3.5.0'
	implementation group: 'com.google.zxing', name: 'core', version: '3.5.0'
	
	// retry
	implementation 'org.springframework.retry:spring-retry'
	
}

subprojects {
	group 'api'
	version '0.0.1-SNAPSHOT'
	sourceCompatibility = '21'
	
	apply plugin: 'java'
	apply plugin: 'java-library'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	
	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}
	
	dependencies {
		// spring web
		implementation 'org.springframework.boot:spring-boot-starter-web'
		
		// lombok
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		
		// etc
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testImplementation 'org.springframework.security:spring-security-test'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	}
	
	repositories {
		mavenCentral()
	}
	
	tasks.named("bootJar") {
	    enabled = false
	}

	def isCi = System.getenv("CI") == "true"
	
	// CI만 Test task 설정
	if (isCi) {
		tasks.withType(Test).configureEach {
			useJUnitPlatform()
			systemProperty "spring.profiles.active", "ci"
		}
	} else {
		// 로컬에선 test task 비활성화
		tasks.matching { it.name == 'test' }.configureEach {
			enabled = false
		}
	}
}